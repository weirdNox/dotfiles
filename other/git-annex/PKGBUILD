pkgname=nox-git-annex
pkgver=8.20210224
pkgrel=1
pkgdesc="Manage files with git, without checking their contents into git."
url="https://git-annex.branchable.com/"
arch=(x86_64)
license=('GPL')
provides=('git-annex')
conflicts=('git-annex')
source=("https://downloads.kitenet.net/git-annex/linux/current/git-annex-standalone-amd64.tar.gz"
        "https://downloads.kitenet.net/git-annex/linux/current/git-annex-standalone-amd64.tar.gz.sig"
        "https://downloads.kitenet.net/git-annex/linux/current/git-annex-standalone-amd64.tar.gz.info")
md5sums=('SKIP' 'SKIP' 'SKIP')
depends=("file" "git" "lsof" "rsync" "sqlite")
validpgpkeys=("40055C6AFD2D526B2961E78F5EE1DBA789C809CB")

pkgver() {
    awk 'NR==4' 'git-annex-standalone-amd64.tar.gz.info'
}

package() {
    cd "$srcdir/git-annex.linux"

    for exe in git-annex git-annex-shell;
    do
        install -Dm755 shimmed/$exe/$exe "$pkgdir/usr/share/$pkgname/bin/$exe"
        install -d "$pkgdir/usr/bin"
        cat > "$pkgdir/usr/bin/$exe" <<EOF
#!/bin/bash
export LD_LIBRARY_PATH="/usr/share/$pkgname/lib":\$LD_LIBRARY_PATH
exec "/usr/share/$pkgname/bin/$exe" "\$@"
EOF
        chmod +x "$pkgdir/usr/bin/$exe"
    done

    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -Dm644 logo.svg "$pkgdir/usr/share/pixmaps/git-annex.svg"
    install -Dm644 logo_16x16.png "$pkgdir/usr/share/pixmaps/git-annex_16x16.png"

    for f in usr/share/man/man1/*.1; do
        install -Dm644 $f "$pkgdir/$f"
    done
}
